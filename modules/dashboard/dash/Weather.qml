import qs.components
import qs.services
import qs.config
import qs.utils
import QtQuick

Item {
    id: root

    anchors.centerIn: parent

    implicitWidth: icon.implicitWidth + info.implicitWidth + info.anchors.leftMargin

    Component.onCompleted: Weather.reload()

    MaterialIcon {
        id: icon

        anchors.verticalCenter: parent.verticalCenter
        anchors.left: parent.left

        animate: true
        text: Weather.error ? "cloud_alert" : Weather.icon
        color: Weather.error ? Colours.palette.m3error : Colours.palette.m3secondary
        font.pointSize: Appearance.font.size.headlineLarge * 2
    }

    Column {
        id: info

        anchors.verticalCenter: parent.verticalCenter
        anchors.left: icon.right
        anchors.leftMargin: Appearance.spacing.xxl

        spacing: Appearance.spacing.sm

        StyledText {
            anchors.horizontalCenter: parent.horizontalCenter

            animate: true
            text: Weather.error ? Weather.error : Weather.temp
            color: Weather.error ? Colours.palette.m3error : Colours.palette.m3primary
            font.pointSize: Weather.error ? Appearance.font.size.bodyMedium : Appearance.font.size.headlineLarge
            font.weight: 500
        }

        StyledText {
            anchors.horizontalCenter: parent.horizontalCenter
            visible: !Weather.error

            animate: true
            text: Weather.description

            elide: Text.ElideRight
            width: Math.min(implicitWidth, root.parent.width - icon.implicitWidth - info.anchors.leftMargin - Appearance.padding.xl * 2)
        }

        StyledText {
            anchors.horizontalCenter: parent.horizontalCenter
            visible: !Weather.error && Weather.city !== ""

            animate: true
            text: Weather.city
            color: Colours.palette.m3onSurfaceVariant
            font.pointSize: Appearance.font.size.labelSmall
            font.weight: 400

            elide: Text.ElideRight
            width: Math.min(implicitWidth, root.parent.width - icon.implicitWidth - info.anchors.leftMargin - Appearance.padding.xl * 2)
        }
    }
}
