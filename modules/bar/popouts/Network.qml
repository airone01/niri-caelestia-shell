pragma ComponentBehavior: Bound

import qs.components
import qs.components.controls
import qs.services
import qs.config
import qs.utils
import Quickshell
import QtQuick
import QtQuick.Layouts

ColumnLayout {
    id: root

    property string connectingToSsid: ""

    spacing: Appearance.spacing.sm
    width: Config.bar.sizes.networkWidth

    StyledText {
        Layout.topMargin: Appearance.padding.md
        Layout.rightMargin: Appearance.padding.xs
        text: qsTr("Wifi %1").arg(Network.wifiEnabled ? "enabled" : "disabled")
        font.weight: 500
    }

    Toggle {
        label: qsTr("Enabled")
        checked: Network.wifiEnabled
        toggle.onToggled: Network.enableWifi(checked)
    }

    StyledText {
        Layout.topMargin: Appearance.spacing.sm
        Layout.rightMargin: Appearance.padding.xs
        text: qsTr("%1 networks available").arg(Network.networks.length)
        color: Colours.palette.m3onSurfaceVariant
        font.pointSize: Appearance.font.size.labelLarge
    }

    Repeater {
        model: ScriptModel {
            values: [...Network.networks].sort((a, b) => {
                if (a.active !== b.active)
                    return b.active - a.active;
                return b.strength - a.strength;
            }).slice(0, 8)
        }

        ColumnLayout {
            id: networkItem

            required property Network.AccessPoint modelData
            readonly property bool isConnecting: root.connectingToSsid === modelData.ssid

            RowLayout {
                Layout.fillWidth: true
                Layout.rightMargin: Appearance.padding.xs
                spacing: Appearance.spacing.sm

                opacity: 0
                scale: 0.7

                Component.onCompleted: {
                    opacity = 1;
                    scale = 1;
                }

                Behavior on opacity {
                    Anim {}
                }

                Behavior on scale {
                    Anim {}
                }

                MaterialIcon {
                    text: Icons.getNetworkIcon(networkItem.modelData.strength)
                    color: networkItem.modelData.active ? Colours.palette.m3primary : Colours.palette.m3onSurfaceVariant
                }

                MaterialIcon {
                    visible: networkItem.modelData.isSecure
                    text: "lock"
                    font.pointSize: Appearance.font.size.labelLarge
                }

                StyledText {
                    Layout.leftMargin: Appearance.spacing.sm / 2
                    Layout.rightMargin: Appearance.spacing.sm / 2
                    Layout.fillWidth: true
                    text: networkItem.modelData.ssid
                    elide: Text.ElideRight
                    font.weight: networkItem.modelData.active ? 500 : 400
                    color: networkItem.modelData.active ? Colours.palette.m3primary : Colours.palette.m3onSurface
                }

                StyledRect {
                    id: connectBtn

                    implicitWidth: implicitHeight
                    implicitHeight: connectIcon.implicitHeight + Appearance.padding.xs

                    radius: Appearance.rounding.full
                    color: Qt.alpha(Colours.palette.m3primary, networkItem.modelData.active ? 1 : 0)

                    StyledBusyIndicator {
                        anchors.fill: parent
                        running: networkItem.isConnecting
                    }

                    StateLayer {
                        color: networkItem.modelData.active ? Colours.palette.m3onPrimary : Colours.palette.m3onSurface
                        disabled: networkItem.isConnecting || !Network.wifiEnabled

                        function onClicked(): void {
                            if (networkItem.modelData.active) {
                                Network.disconnectFromNetwork();
                            } else {
                                root.connectingToSsid = networkItem.modelData.ssid;
                                Network.connectToNetwork(root.connectingToSsid);
                            }
                        }
                    }

                    MaterialIcon {
                        id: connectIcon

                        anchors.centerIn: parent
                        animate: true
                        text: networkItem.modelData.active ? "link_off" : "link"
                        color: networkItem.modelData.active ? Colours.palette.m3onPrimary : Colours.palette.m3onSurface

                        opacity: networkItem.isConnecting ? 0 : 1

                        Behavior on opacity {
                            Anim {}
                        }
                    }
                }
            }

            // Password entry section
            StyledRect {
                id: askWifiPassword
                visible: networkItem.isConnecting && Network.isconnectionFailed

                Layout.rightMargin: Appearance.padding.xs
                Layout.fillWidth: true
                implicitHeight: confirmPswdIcon.implicitHeight + Appearance.padding.xs * 2

                color: Colours.palette.m3surfaceContainerHighest
                radius: Appearance.rounding.large

                RowLayout {
                    anchors.fill: parent
                    anchors.margins: Appearance.padding.xs / 2
                    spacing: Appearance.spacing.sm

                    opacity: 0
                    scale: 0.7

                    Component.onCompleted: {
                        opacity = 1;
                        scale = 1;
                    }

                    // Show/hide password button
                    StyledRect {
                        id: hidePswdBtn
                        property bool isclicked: false

                        Layout.leftMargin: Appearance.padding.xs / 2
                        implicitWidth: implicitHeight
                        implicitHeight: hidePswdIcon.implicitHeight + Appearance.padding.xs

                        radius: Appearance.rounding.full
                        color: Qt.alpha(Colours.palette.m3primary, hidePswdBtn.isclicked ? 1 : 0)

                        StateLayer {
                            disabled: false

                            function onClicked(): void {
                                hidePswdBtn.isclicked = !hidePswdBtn.isclicked;
                            }
                        }

                        MaterialIcon {
                            id: hidePswdIcon

                            anchors.centerIn: parent
                            animate: true
                            text: hidePswdBtn.isclicked ? "visibility_off" : "visibility"
                            color: askWifiPassword.visible && hidePswdBtn.isclicked ? Colours.palette.m3onPrimary : Colours.palette.m3onSurface
                        }
                    }

                    // Password input field
                    StyledTextField {
                        id: wifiPasswordField
                        Layout.leftMargin: Appearance.spacing.sm / 2
                        Layout.rightMargin: Appearance.spacing.sm / 2
                        Layout.fillWidth: true
                        placeholderText: qsTr("Enter Password")
                        passwordMaskDelay: 300
                        echoMode: hidePswdBtn.isclicked ? TextInput.Normal : TextInput.Password
                        selectByMouse: true
                        focus: askWifiPassword.visible
                        onActiveFocusChanged: {
                            if (!activeFocus && askWifiPassword.visible)
                                forceActiveFocus();
                        }
                        Keys.onReturnPressed: {
                            if (text.length > 0) {
                                Network.connectToSecureNetwork(root.connectingToSsid, text);
                                text = "";
                            }
                        }
                        Keys.onEscapePressed: {
                            Network.isconnectionFailed = false;
                            root.connectingToSsid = "";
                            text = "";
                        }
                    }

                    // Confirm button
                    StyledRect {
                        id: confirmPswdBtn
                        property bool isclicked: false

                        implicitWidth: implicitHeight
                        implicitHeight: confirmPswdIcon.implicitHeight + Appearance.padding.xs

                        radius: Appearance.rounding.full
                        color: Qt.alpha(Colours.palette.m3primary, confirmPswdBtn.isclicked ? 1 : 0)

                        StateLayer {
                            disabled: wifiPasswordField.text.length === 0

                            function onClicked(): void {
                                confirmPswdBtn.isclicked = true;
                                Network.connectToSecureNetwork(root.connectingToSsid, wifiPasswordField.text);
                                wifiPasswordField.text = "";
                                confirmwaitTimer.start();
                            }
                        }

                        MaterialIcon {
                            id: confirmPswdIcon

                            anchors.centerIn: parent
                            animate: true
                            text: "check"
                            color: askWifiPassword.visible && confirmPswdBtn.isclicked ? Colours.palette.m3onPrimary : Colours.palette.m3onSurface
                        }

                        Timer {
                            id: confirmwaitTimer
                            interval: 100
                            repeat: false
                            onTriggered: {
                                confirmPswdBtn.isclicked = false;
                            }
                        }
                    }

                    // Cancel button
                    StyledRect {
                        id: cancelPswdBtn
                        property bool isclicked: false

                        Layout.rightMargin: Appearance.spacing.sm / 2
                        implicitWidth: implicitHeight
                        implicitHeight: cancelPswdIcon.implicitHeight + Appearance.padding.xs

                        radius: Appearance.rounding.full
                        color: Qt.alpha(Colours.palette.m3primary, cancelPswdBtn.isclicked ? 1 : 0)

                        StateLayer {
                            disabled: false

                            function onClicked(): void {
                                cancelPswdBtn.isclicked = true;
                                cancelwaitTimer.start();
                            }
                        }

                        MaterialIcon {
                            id: cancelPswdIcon

                            anchors.centerIn: parent
                            animate: true
                            text: "close"
                            color: askWifiPassword.visible && cancelPswdBtn.isclicked ? Colours.palette.m3onPrimary : Colours.palette.m3onSurface
                        }

                        Timer {
                            id: cancelwaitTimer
                            interval: 100
                            repeat: false
                            onTriggered: {
                                cancelPswdBtn.isclicked = false;
                                Network.isconnectionFailed = false;
                                root.connectingToSsid = "";
                                wifiPasswordField.text = "";
                            }
                        }
                    }
                }

                onVisibleChanged: {
                    if (!visible) {
                        hidePswdBtn.isclicked = false;
                        wifiPasswordField.text = "";
                    }
                }
            }
        }
    }

    StyledRect {
        Layout.topMargin: Appearance.spacing.sm
        Layout.fillWidth: true
        implicitHeight: rescanBtn.implicitHeight + Appearance.padding.xs * 2

        radius: Appearance.rounding.full
        color: Colours.palette.m3primaryContainer

        StateLayer {
            color: Colours.palette.m3onPrimaryContainer
            disabled: Network.scanning || !Network.wifiEnabled

            function onClicked(): void {
                Network.rescanWifi();
            }
        }

        RowLayout {
            id: rescanBtn

            anchors.centerIn: parent
            spacing: Appearance.spacing.sm
            opacity: Network.scanning ? 0 : 1

            MaterialIcon {
                id: scanIcon

                animate: true
                text: "wifi_find"
                color: Colours.palette.m3onPrimaryContainer
            }

            StyledText {
                text: qsTr("Rescan networks")
                color: Colours.palette.m3onPrimaryContainer
            }

            Behavior on opacity {
                Anim {}
            }
        }

        StyledBusyIndicator {
            anchors.centerIn: parent
            strokeWidth: Appearance.padding.xs / 2
            bgColour: "transparent"
            implicitHeight: parent.implicitHeight - Appearance.padding.sm * 2
            running: Network.scanning
        }
    }

    // Reset connecting state when network changes
    Connections {
        target: Network

        function onActiveChanged(): void {
            if (Network.active && root.connectingToSsid === Network.active.ssid) {
                root.connectingToSsid = "";
            }
        }

        function onScanningChanged(): void {
            if (!Network.scanning)
                scanIcon.rotation = 0;
        }
    }

    component Toggle: RowLayout {
        required property string label
        property alias checked: toggle.checked
        property alias toggle: toggle

        Layout.fillWidth: true
        Layout.rightMargin: Appearance.padding.xs
        spacing: Appearance.spacing.lg

        StyledText {
            Layout.fillWidth: true
            text: parent.label
        }

        StyledSwitch {
            id: toggle
        }
    }
}
